(ns phel-aa\image-reader
  (:require phel\str)
  (:require phel-aa\char-block)
  (:require phel-aa\similar-chars))

(defstruct image [im w h scale])

(defn create-image [filename & [scale]]
  (let [[w h] (php/getimagesize filename)
        ext (php/pathinfo filename php/PATHINFO_EXTENSION)
        img (case ext
              "jpeg" (php/imagecreatefromjpeg filename)
              "jpg" (php/imagecreatefromjpeg filename)
              "png"  (php/imagecreatefrompng filename)
              (throw (php/new \Exception "invalid file extension.")))]
    (image img w h (or scale 1))))

(defstruct char-cell [w h scale])

(defn get-char-cells [image]
  (let [{:w w :h h :scale scale} image
        char-cell-height (* (+ char-block/minimum-size char-block/minimum-mergin-height) scale)
        char-cell-width (* (+ char-block/minimum-size char-block/minimum-mergin-width) scale)]
    (for [h :range [0 (php/intval (/ h char-cell-height))]]
      (for [w :range [0 (php/intval (/ w char-cell-width))]]
        (char-cell w h scale)))))

(defn convert-to-hist [image char-cell]
  (let [points (char-block/get-minimum-points)
        {:scale scale} image
        char-cell-height (* (+ char-block/minimum-size char-block/minimum-mergin-height) scale)
        char-cell-width (* (+ char-block/minimum-size char-block/minimum-mergin-width) scale)
        w-offset (php/intval (* char-cell-width (char-cell :w)))
        h-offset (php/intval (* char-cell-height (char-cell :h)))]
    (map (fn [[x y]]
           (let [rgb (php/imagecolorat
                       (image :im)
                       (+ x w-offset)
                       (+ y h-offset))]
             (char-block/get-rgb-average rgb)))
         points)))

(defn convert [dict image char-cells]
  (str/join "\n" (map (fn [row]
                        (str/join "" (map (fn [char-cell]
                                       (let [hist (convert-to-hist image char-cell)]
                                         (similar-chars/get-similar-char dict hist)))
                                     row)))
                      char-cells)))

