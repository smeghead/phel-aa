(ns phel-aa\image-reader
  (:require phel\str)
  (:require phel-aa\image-block)
  (:require phel-aa\similar-chars))

(defstruct image [im])

(defn create-image [filename]
  (image (php/imagecreatefrompng filename)))

(defstruct block-point [w h])

(defn get-block-points [filename]
  (let [[w h] (php/getimagesize filename)]
    (for [h :range [0 (php/intval (/ h image-block/height))]]
      (for [w :range [0 (php/intval (/ w image-block/width))]]
        (block-point w h)))))

(defn convert-to-hist [image block-point]
  (let [points (image-block/get-points)
        w-offset (* image-block/width (block-point :w))
        h-offset (* image-block/height (block-point :h))]
    (map (fn [[x y]]
           (let [rgb (php/imagecolorat
                       (image :im)
                       (+ x w-offset)
                       (+ y h-offset))]
             (image-block/get-rgb-average rgb)))
         points)))

(defn convert [dict image block-points]
  (str/join "\n" (map (fn [row]
                        (str/join "" (map (fn [block-point]
                                       (let [hist (convert-to-hist image block-point)]
                                         (similar-chars/get-similar-char dict hist)))
                                     row)))
                      block-points)))

