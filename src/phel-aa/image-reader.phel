(ns phel-aa\image-reader
  (:require phel\str)
  (:require phel-aa\char-block)
  (:require phel-aa\similar-chars))

(defstruct image [im])

(defn create-image [filename]
  (image (php/imagecreatefrompng filename)))

(defstruct block-position [w h])

(defn get-block-points [filename]
  (let [[w h] (php/getimagesize filename)]
    (for [h :range [0 (php/intval (/ h char-block/height))]]
      (for [w :range [0 (php/intval (/ w char-block/width))]]
        (block-position w h)))))

(defn convert-to-hist [image block-position]
  (let [points (char-block/get-points)
        w-offset (* char-block/width (block-position :w))
        h-offset (* char-block/height (block-position :h))]
    (map (fn [[x y]]
           (let [rgb (php/imagecolorat
                       (image :im)
                       (+ x w-offset)
                       (+ y h-offset))]
             (char-block/get-rgb-average rgb)))
         points)))

(defn convert [dict image block-points]
  (str/join "\n" (map (fn [row]
                        (str/join "" (map (fn [block-position]
                                       (let [hist (convert-to-hist image block-position)]
                                         (similar-chars/get-similar-char dict hist)))
                                     row)))
                      block-points)))

