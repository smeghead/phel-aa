(ns phel-aa\image-reader
  (:require phel\str)
  (:require phel-aa\char-block)
  (:require phel-aa\similar-chars))

(defstruct image [im])

(defn create-image [filename]
  (image (php/imagecreatefrompng filename)))

(defstruct char-cell [w h scale])

(defn get-char-cells [filename]
  (let [[w h] (php/getimagesize filename)
        scale (/ w 80 char-block/width)]
    (for [h :range [0 (php/intval (/ h (* char-block/height scale)))]]
      (for [w :range [0 (php/intval (/ w (* char-block/width scale)))]]
        (char-cell w h scale)))))

(defn convert-to-hist [image char-cell]
  (let [points (char-block/get-points)
        w-offset (php/intval (* (* char-block/width (char-cell :scale)) (char-cell :w)))
        h-offset (php/intval (* (* char-block/height (char-cell :scale)) (char-cell :h)))]
    (map (fn [[x y]]
           (let [rgb (php/imagecolorat
                       (image :im)
                       (+ x w-offset)
                       (+ y h-offset))]
             (char-block/get-rgb-average rgb)))
         points)))

(defn convert [dict image char-cells]
  (str/join "\n" (map (fn [row]
                        (str/join "" (map (fn [char-cell]
                                       (let [hist (convert-to-hist image char-cell)]
                                         (similar-chars/get-similar-char dict hist)))
                                     row)))
                      char-cells)))

